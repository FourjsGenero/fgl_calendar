ACTION DEFAULTS
  ACTION calendar_selection (DEFAULTVIEW=NO)
END
LAYOUT (TEXT="FGL Calendar demo", STYLE="main2")
VBOX
GRID
{
[bl|cm         |cy   |br]
[cal                    ]
[                       ]
[                       ]
[                       ]
[                       ]
[                       ]
[                       ]
[                       ]
}
END
HBOX
GRID
{
 Type: [ct              ]
 Theme:[cc              ]
 Mode: [sm              ]
}
END
GRID
{
[sdn                    ]
[sdu                    ]
[swn                    ]
}
END
GRID
{
 Date 1: [cd1           ]
 Date 2: [cd2           ]
         [b1            ]
         [b2            ]
}
END
END
END

ATTRIBUTES

BUTTON bl: year_month_decr, IMAGE="fa-caret-left";
COMBOBOX cm=FORMONLY.curr_month, NOT NULL,
   STYLE="bigfont",
   INITIALIZER=cmb_init_month;
SPINEDIT cy=FORMONLY.curr_year, NOT NULL,
   VALUEMIN=1900, VALUEMAX=2200,
   STYLE="bigfont";
BUTTON br: year_month_incr, IMAGE="fa-caret-right";

WEBCOMPONENT cal=FORMONLY.calendar, TABINDEX=0,
   COMPONENTTYPE="fglsvgcanvas",
   PROPERTIES=(selection="calendar_selection"),
   SIZEPOLICY=FIXED,
   STRETCH=BOTH,
   SCROLLBARS=NONE
;

COMBOBOX ct=FORMONLY.curr_type, NOT NULL,
   INITIALIZER=cmb_init_types;
COMBOBOX cc=FORMONLY.curr_theme, NOT NULL,
   INITIALIZER=cmb_init_themes;
COMBOBOX sm=FORMONLY.curr_mode, NOT NULL,
   INITIALIZER=cmb_init_modes;

CHECKBOX sdn=FORMONLY.show_daynames, NOT NULL,
   TEXT="Show day names";
CHECKBOX sdu=FORMONLY.show_daynums, NOT NULL,
   TEXT="Show day numbers";
CHECKBOX swn=FORMONLY.show_weeknums, NOT NULL,
   TEXT="Show week numbers";

DATEEDIT cd1=FORMONLY.selected_date_1;
DATEEDIT cd2=FORMONLY.selected_date_2;
BUTTON b1: clear, TEXT="Clear";
BUTTON b2: close, TEXT="Close";

END
